plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

allprojects {
    version = '1.0.2'
    group = 'top.mrxiaom'
    description = "修复离线模式下无法进入多人游戏、发送聊天的问题"
    apply plugin: 'java'

    repositories {
        maven { url = "https://maven.fastmirror.net/repositories/minecraft/" }
        maven { url = "https://repo.huaweicloud.com/repository/maven/" }
        mavenCentral()
    }
}
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}
loom.mixin {
    defaultRefmapName.set(mixin_refmap)
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    compileOnly "net.minecraftforge:forge:1.16.5-36.1.4:universal"

}

jar {
    def build2 = project(":authlib2").tasks.jar
    def build3 = project(":authlib3").tasks.jar
    dependsOn(build2)
    dependsOn(build3)
    build2.outputs.files.forEach { from(zipTree(it)) }
    build3.outputs.files.forEach { from(zipTree(it)) }
}

build {
    archivesBaseName += '-forge-fabric'
}


processResources {
    from "${project.rootDir}/LICENSE"

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand(
                'name': rootProject.name,
                'description': description,
                'version': version
        )
        include 'fabric.mod.json'
        include 'META-INF/mods.toml'
    }
}
