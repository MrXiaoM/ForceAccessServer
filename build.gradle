plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

allprojects {
    version = '1.0.2'
    group = 'top.mrxiaom'
    description = "修复离线模式下无法进入多人游戏、发送聊天的问题"
    apply plugin: 'java'

    repositories {
        maven { url = "https://maven.fastmirror.net/repositories/minecraft/" }
        maven { url = "https://repo.huaweicloud.com/repository/maven/" }
        mavenCentral()
    }
}
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}
loom.mixin {
    defaultRefmapName.set(mixin_refmap)
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    compileOnly "net.minecraftforge:forge:1.16.5-36.1.4:universal"

}

jar {
    archivesBaseName += '-forge-fabric'
    ["authlib2", "authlib3_3", "authlib3_18"].stream().map {
        project(it).tasks.jar
    }.flatMap {
        dependsOn(it)
        it.outputs.files.files.stream()
    }.forEach {
        from(zipTree(it))
    }
}

processResources {
    from "${project.rootDir}/LICENSE"

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand(
                'name': rootProject.name,
                'description': description,
                'version': version
        )
        include 'fabric.mod.json'
        include 'META-INF/mods.toml'
    }
}
